plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

archivesBaseName = 'apoc-hadoop-dependencies'
description = """APOC Apache Hadoop Dependencies"""

jar {
    manifest {
        attributes 'Implementation-Version': version
    }
}

/*
we use mergeServiceFiles() to exclude commons classes like org.apache.hadoop.fs.FileSystem, 
coming from both hadoop-hdfs-client and hadoop-common
alternatively, we have to set into HdfsConfiguration
conf.set("fs.hdfs.impl", "org.apache.hadoop.hdfs.DistributedFileSystem");
conf.set("fs.file.impl", "org.apache.hadoop.fs.LocalFileSystem");
*/
shadowJar {
    mergeServiceFiles()
}

def commonExclusions = {
    exclude group: 'io.netty'
    exclude group: 'com.sun.jersey'
    exclude group: 'org.eclipse.jetty'
    exclude group: 'org.eclipse.jetty.aggregate'
    exclude group: 'org.slf4j', module: 'slf4j-api'
}

dependencies {
    implementation group: 'org.apache.hadoop', name: 'hadoop-hdfs-client', version: '3.3.5', commonExclusions
    implementation group: 'org.apache.hadoop', name: 'hadoop-common', version: '3.3.5', commonExclusions
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.23.1', commonExclusions

    implementation group: 'org.apache.parquet', name: 'parquet-hadoop', version: '1.13.1', commonExclusions
    implementation group: 'org.apache.parquet', name: 'parquet-column', version: '1.13.1', commonExclusions
    implementation group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: '3.3.5', commonExclusions
}




