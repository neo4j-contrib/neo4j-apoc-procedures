import org.gradle.api.internal.artifacts.DefaultExcludeRule

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
    id "com.bmuschko.nexus"
    id 'maven-publish'
    id 'antlr'
    id "org.sonarqube"
    id "org.jetbrains.kotlin.jvm" version "1.6.0"
    id "com.diffplug.spotless" version "6.7.2"
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        removeUnusedImports()
    }
}

archivesBaseName = "apoc"


jar {
    manifest {
        attributes 'Implementation-Version': version
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

generateGrammarSource {
    arguments += ["-package", "apoc.custom"]
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
}

shadowJar {
    mergeServiceFiles()
    archiveClassifier.set('all')
}

dependencies {
    compile project(":core")
    apt project(":processor")
    testCompile project(':test-utils')

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib"
    apt group: 'org.neo4j', name: 'neo4j', version: neo4jVersionEffective   // mandatory to run @ServiceProvider based META-INF code generation
//    compile group: 'commons-codec', name: 'commons-codec', version: '1.14'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
    compile group: 'org.hdrhistogram', name: 'HdrHistogram', version: '2.1.9'
    compileOnly group: 'org.neo4j.driver', name: 'neo4j-java-driver', version: '4.4.5'
    compile group: 'com.novell.ldap', name: 'jldap', version: '2009-10-07'

    antlr "org.antlr:antlr4:4.7.2", {
        exclude group: 'org.glassfish'
        exclude group: 'com.ibm.icu'
        exclude group: 'org.abego.treelayout'
    }
    // compileOnly "org.antlr:antlr4-runtime:4.7.2"
    // testCompile "org.antlr:antlr4-runtime:4.7.2"

    compileOnly "org.apache.poi:poi:5.1.0"
    testCompile "org.apache.poi:poi:5.1.0"
    compileOnly "org.apache.poi:poi-ooxml:5.1.0"
    testCompile "org.apache.poi:poi-ooxml:5.1.0"

    compile 'org.jsoup:jsoup:1.14.3'

    // currently we cannot update selenium to the latest version due to guava minimum version required (31.0.1-jre)
    compileOnly group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59', { exclude group: 'com.google.guava', module: 'guava' }
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59', { exclude group: 'com.google.guava', module: 'guava' }
    compileOnly group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.0.3'
    testCompile group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.0.3'

    compile group: 'org.roaringbitmap', name: 'RoaringBitmap', version: '0.7.17'
    compile(group: 'org.apache.commons', name: 'commons-configuration2', version: '2.7') {
        exclude group: "org.yaml"
        exclude module: "snakeyaml"
    }
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.26'
    testCompile group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'

    testCompile 'net.sourceforge.jexcelapi:jxl:2.6.12'

    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.270'
    testCompile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.270'

    testCompile group: 'org.reflections', name: 'reflections', version: '0.9.12'

    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.neo4j.test', name: 'neo4j-harness', version: neo4jVersionEffective
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.22.v20170606'
    testCompile group: 'org.apache.derby', name: 'derby', version: '10.12.1.1'

    testCompile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'

    def withoutServers = {
        exclude group: 'org.eclipse.jetty'
        exclude group: 'org.eclipse.jetty.aggregate'
        exclude group: 'org.apache.hive', module: 'hive-service'
    }

    def withoutJacksons = {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }

    compileOnly 'org.mongodb:mongodb-driver:3.2.2', {
        exclude group: 'io.netty'
    }
    testCompile 'org.mongodb:mongodb-driver:3.2.2', {
        exclude group: 'io.netty'
    }

    compileOnly group: 'com.couchbase.client', name: 'java-client', version: '3.3.0', withoutJacksons
    testCompile group: 'com.couchbase.client', name: 'java-client', version: '3.3.0', withoutJacksons

    compileOnly group: 'io.lettuce', name: 'lettuce-core', version: '6.1.1.RELEASE'
    testCompile group: 'io.lettuce', name: 'lettuce-core', version: '6.1.1.RELEASE'

    compileOnly group: 'org.neo4j', name: 'neo4j', version: neo4jVersionEffective

    compileOnly group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.13.2', withoutJacksons
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0'

    testCompile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.13.2', withoutJacksons
    testCompile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0'

    testCompile 'org.mock-server:mockserver-netty:5.6.0'
    testCompile 'org.mock-server:mockserver-client-java:5.6.0'

    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-comprehend', version: '1.12.214' , withoutJacksons
    testImplementation group: 'com.amazonaws', name: 'aws-java-sdk-comprehend', version: '1.12.214' , withoutJacksons

    compile group: 'com.opencsv', name: 'opencsv', version: '4.6'
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    compileOnly group: 'org.ow2.asm', name: 'asm', version: '5.0.2'
    compile group: 'com.github.javafaker', name: 'javafaker', version: '1.0.2'

    // schemacrawler
    compile group: 'us.fatehi', name: 'schemacrawler', version: '15.04.01'
    testCompile group: 'us.fatehi', name: 'schemacrawler-mysql', version: '15.04.01'

    testCompile group: 'org.apache.hive', name: 'hive-jdbc', version: '1.2.2', withoutServers

    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

    testCompile group: 'org.xmlunit', name: 'xmlunit-core', version: '2.2.1'
    testCompile group: 'com.github.adejanovski', name: 'cassandra-jdbc-wrapper', version: '3.1.0'


    testCompile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.13.2'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.13.2'

    compileOnly group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.6.1'
    testCompile group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.6.1', {
        exclude group: 'com.google.guava', module: 'guava'
    }

    compileOnly group: 'com.sun.mail', name: 'javax.mail', version: '1.6.0'
    testCompile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.0'

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
}



// tweaks for CI
if (System.env.CI == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'pipeline'
            url = "file://${project(':full').buildDir}/repo"
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            artifactId("apoc")
            project.shadow.component(publication)
            artifact(mySourcesJar)
            artifact(myJavadocJar)
            artifact(jar)

            pom.withXml {
                def root = asNode()
                root.appendNode("name", "neo4j-apoc-procedure")
                root.appendNode("description", "A collection of useful Neo4j Procedures")
                root.appendNode("url", "http://github.com/neo4j-contrib/neo4j-apoc-procedures")

                def scmNode = root.appendNode("scm")
                scmNode.appendNode("url", "http://github.com/neo4j-contrib/neo4j-apoc-procedures")

                def licensesNode = root.appendNode("licenses")
                def licenseNode = licensesNode.appendNode("license")
                licenseNode.appendNode("name", 'The Apache Software License, Version 2.0')
                licenseNode.appendNode("url", 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenseNode.appendNode("comments", 'Note that this license is for the project itself, and not for its dependencies.')
                licenseNode.appendNode("distribution", 'repo')

                def developersNode = root.appendNode("developers")
                def developerNode = developersNode.appendNode("developer")
                developerNode.appendNode("id", 'michaelhunger')
                developerNode.appendNode("name", 'Michael Hunger')
                developerNode.appendNode("email", 'michael.hunger@neo4j.com')

                def dependenciesNode = root.get("dependencies")[0]

                configurations.default.allDependencies.findAll {dependency -> dependency.group != "org.neo4j.procedure"}.forEach {
                    def dependency = dependenciesNode.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                    dependency.appendNode('scope', "compile")

                    def myExcludeRules = it.excludeRules
                    if (myExcludeRules.size() == 0) {
                        myExcludeRules = new HashSet<>()
                    }

                    myExcludeRules.add(new DefaultExcludeRule("org.slf4j", "slf4j-nop"))

                    // for exclusions
                    if (myExcludeRules.size() > 0) {
                        def exclusions = dependency.appendNode('exclusions')
                        myExcludeRules.each { ExcludeRule ex ->
                            def exclusion = exclusions.appendNode('exclusion')
                            exclusion.appendNode('artifactId', ex.module ? ex.module : "*")
                            exclusion.appendNode('groupId', ex.group)
                        }
                    }

                }

            }

        }
    }
}

