plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' apply true
}

base {
    archivesName = "apoc"
}

jar {
    manifest {
        attributes 'Implementation-Version': archiveVersion
    }
}


configurations {
    apt
}

shadowJar {
    archiveClassifier.set('core')
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
}


dependencies {

    testImplementation project(':test-utils')
    apt project(':processor')

    apt group: 'org.neo4j', name: 'neo4j', version: neo4jVersionEffective
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.16.1'
    // when json-path is changed, it have to be changed in json-path-version version in antora.yml as well.
    implementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.9.0'
    implementation group: 'org.hdrhistogram', name: 'HdrHistogram', version: '2.1.9'

    constraints {
        // Remove when json-path has updated transitive dependency
        api('net.minidev:json-smart:2.5.2') {
            because 'CVE-2024-57699'
        }
    }

    // compileOnly "org.antlr:antlr4-runtime:4.7.2"
    // testCompile "org.antlr:antlr4-runtime:4.7.2"

    implementation group: 'org.roaringbitmap', name: 'RoaringBitmap', version: '0.7.17'
    implementation(group: 'org.apache.commons', name: 'commons-configuration2', version: '2.10.1') {
        exclude group: "org.yaml"
        exclude module: "snakeyaml"
        exclude module: "commons-lang3"
        exclude module: "commons-text"
    }

    // This was reported here https://github.com/neo4j-contrib/neo4j-apoc-procedures/issues/3048
    implementation(group: 'org.apache.commons', name: 'commons-lang3') {
        version {
            strictly '3.14.0'
        }
    }
    implementation group: 'com.github.seancfoley', name: 'ipaddress', version: '5.3.3'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'

    testImplementation 'net.sourceforge.jexcelapi:jxl:2.6.12'

    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.12.770'

    testImplementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
    testImplementation group: 'org.neo4j.test', name: 'neo4j-harness', version: neo4jVersionEffective
    testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.22.v20170606'
    testImplementation group: 'org.apache.derby', name: 'derby', version: '10.14.2.0'

    testImplementation group: 'org.neo4j', name: 'neo4j-common', version: neo4jVersionEffective, classifier: "tests"
    testImplementation group: 'org.neo4j.community', name: 'it-test-support', version: neo4jVersionEffective // , classifier: "tests"
    testImplementation group: 'org.neo4j', name: 'log-test-utils', version: neo4jVersionEffective // , classifier: "tests"


    testImplementation group: 'org.neo4j', name: 'neo4j-kernel', version: neo4jVersionEffective, classifier: "tests"
    testImplementation group: 'org.neo4j', name: 'neo4j-io', version: neo4jVersionEffective, classifier: "tests"


    def withoutServers = {
        exclude group: 'org.eclipse.jetty'
        exclude group: 'org.eclipse.jetty.aggregate'
        exclude group: 'org.apache.hive', module: 'hive-service'
        exclude group: 'com.google.guava', module: 'guava'
    }
    
    def withoutJacksons = {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }

    compileOnly group: 'org.neo4j', name: 'neo4j', version: neo4jVersionEffective

    testImplementation 'org.mock-server:mockserver-netty:5.15.0'
    testImplementation 'org.mock-server:mockserver-client-java:5.15.0'

    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-comprehend', version: '1.12.770' , withoutJacksons
    testImplementation group: 'com.amazonaws', name: 'aws-java-sdk-comprehend', version: '1.12.770' , withoutJacksons

    implementation group: 'com.opencsv', name: 'opencsv', version: '5.7.1'
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    compileOnly group: 'org.ow2.asm', name: 'asm', version: '5.0.2'

    testImplementation group: 'org.apache.hive', name: 'hive-jdbc', version: '1.2.2', withoutServers

    compileOnly group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: '3.4.0', withoutServers
    // If updated check if the transitive dependency to javax.servlet.jsp:jsp-api:2.1 has also updated
    // and remove the manual licensing check for it in licenses-3rdparties.gradle
    compileOnly group: 'org.apache.hadoop', name: 'hadoop-common', version: '3.4.0', withoutServers

    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    // explicit update commons.io version
    implementation group: 'commons-io', name: 'commons-io', version: '2.17.0'

    compileOnly group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    testImplementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
//    jmh group: 'org.neo4j', name: 'neo4j-lucene-index', version: neo4jVersionEffective
//    jmh group: 'org.neo4j', name: 'neo4j-kernel', version: neo4jVersionEffective, classifier: "tests"

    testImplementation group: 'org.xmlunit', name: 'xmlunit-core', version: '2.2.1'
    testImplementation group: 'com.github.adejanovski', name: 'cassandra-jdbc-wrapper', version: '3.1.0'

    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.13.2'

    compileOnly group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.29.1'
    testImplementation group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.29.1', {
        exclude group: 'com.google.guava', module: 'guava'
    }

    implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'

    implementation group: 'xerces', name: 'xercesImpl', version: '2.12.2'

    def arrowExclusions = {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'io.netty', module: 'netty-common'
        exclude group: 'io.netty', module: 'netty-buffer'
    }

    implementation group: 'org.apache.arrow', name: 'arrow-vector', version: '16.1.0', arrowExclusions
    implementation group: 'org.apache.arrow', name: 'arrow-memory-netty', version: '16.1.0', arrowExclusions

    testImplementation group: 'org.apache.arrow', name: 'arrow-vector', version: '16.1.0'
    testImplementation group: 'org.apache.arrow', name: 'arrow-memory-netty', version: '16.1.0'

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
}

task copyRuntimeLibs(type: Copy) {
    into "lib"
    from configurations.testRuntimeOnly
}