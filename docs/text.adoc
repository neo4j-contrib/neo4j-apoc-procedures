= String Utility Procedures

Basic utility procedures for string manipulation, comparison, filtering etc.

The `replace`, `split` and `regexpGroups` functions work with regular expressions.

The clean functionality can be useful for cleaning up slightly dirty text data with inconsistent formatting for non-exact comparisons.

Cleaning will strip the string of all non-alphanumeric characters (including spaces) and convert it to lower case.

== String Search

The `indexOf` function, provides the fist occurrence of the given lookup string within the text, or -1 if not found.
It can optionally take an offset parameter.

.returns `6`
[source,cypher]
----
RETURN apoc.text.indexOf('Hello World!', 'World')
----

If you want to get a substring starting from your index match, you can use this

.returns `World!`
[source,cypher]
----
WITH 'Hello World!' as text, length(text) as len
WITH text, len, apoc.text.indexOf(text, 'World',3) as index
RETURN substring(text, case index when -1 then len-1 else index end, len);
----

== Regular Expressions

.will return 'HelloWorld'
[source,cypher]
----
RETURN apoc.text.replace('Hello World!', '[^a-zA-Z]', '')
----

[source,cypher]
----
RETURN apoc.text.regexGroups('abc <link xxx1>yyy1</link> def <link xxx2>yyy2</link>','<link (\\w+)>(\\w+)</link>') AS result

// [["<link xxx1>yyy1</link>", "xxx1", "yyy1"], ["<link xxx2>yyy2</link>", "xxx2", "yyy2"]]
----


== Split and Join

.will split with the given regular expression return ['Hello', 'World']
[source,cypher]
----
RETURN apoc.text.split('Hello   World', ' +')
----

.will return 'Hello World'
[source,cypher]
----
RETURN apoc.text.join(['Hello', 'World'], ' ')
----

== Data Cleaning

.will return 'helloworld'
[source,cypher]
----
RETURN apoc.text.clean('Hello World!')
----

.will return `true`
[source,cypher]
----
RETURN apoc.text.compareCleaned('Hello World!', '_hello-world_')
----

.will return only 'Hello World!'
[source,cypher]
----
UNWIND ['Hello World!', 'hello worlds'] as text
RETURN apoc.text.filterCleanMatches(text, 'hello_world') as text
----

== Case Change Functions

=== Capitalise the first letter of the word with `capitalize`

[source,cypher]
----
RETURN apoc.text.capitalize("neo4j") // "Neo4j"
----

=== Capitalise the first letter of every word in the text with `capitalizeAll`

[source,cypher]
----
RETURN apoc.text.capitalizeAll("graph database") // "Graph Database"
----

=== Decapitalize the first letter of the string with `decapitalize`

[source,cypher]
----
RETURN apoc.text.decapitalize("Graph Database") // "graph Database"
----

=== Decapitalize the first letter of all words with `decapitalizeAll`

[source,cypher]
----
RETURN apoc.text.decapitalizeAll("Graph Databases") // "graph databases"
----

=== Swap the case of a string with `swapCase`


[source,cypher]
----
RETURN apoc.text.swapCase("Neo4j") // nEO4J
----

=== Convert a string to lower camelCase with `camelCase`

[source,cypher]
----
RETURN apoc.text.camelCase("FOO_BAR");    // "fooBar"
RETURN apoc.text.camelCase("Foo bar");    // "fooBar"
RETURN apoc.text.camelCase("Foo22 bar");  // "foo22Bar"
RETURN apoc.text.camelCase("foo-bar");    // "fooBar"
RETURN apoc.text.camelCase("Foobar");     // "foobar"
RETURN apoc.text.camelCase("Foo$$Bar");   // "fooBar"
----

=== Convert a string to UpperCamelCase with `upperCamelCase`

[source,cypher]
----
RETURN apoc.text.upperCamelCase("FOO_BAR");   // "FooBar"
RETURN apoc.text.upperCamelCase("Foo bar");   // "FooBar"
RETURN apoc.text.upperCamelCase("Foo22 bar"); // "Foo22Bar"
RETURN apoc.text.upperCamelCase("foo-bar");   // "FooBar"
RETURN apoc.text.upperCamelCase("Foobar");    // "Foobar"
RETURN apoc.text.upperCamelCase("Foo$$Bar");  // "FooBar"
----

=== Convert a string to snake-case with `snakeCase`


[source,cypher]
----
RETURN apoc.text.snakeCase("test Snake Case"); // "test-snake-case"
RETURN apoc.text.snakeCase("FOO_BAR");         // "foo-bar"
RETURN apoc.text.snakeCase("Foo bar");         // "foo-bar"
RETURN apoc.text.snakeCase("fooBar");          // "foo-bar"
RETURN apoc.text.snakeCase("foo-bar");         // "foo-bar"
RETURN apoc.text.snakeCase("Foo bar");         // "foo-bar"
RETURN apoc.text.snakeCase("Foo  bar");        // "foo-bar"
----

=== Convert a string to UPPER_CASE with `toUpperCase

[source,cypher]
----
RETURN apoc.text.toUpperCase("test upper case"); // "TEST_UPPER_CASE"
RETURN apoc.text.toUpperCase("FooBar");          // "FOO_BAR"
RETURN apoc.text.toUpperCase("fooBar");          // "FOO_BAR"
RETURN apoc.text.toUpperCase("foo-bar");         // "FOO_BAR"
RETURN apoc.text.toUpperCase("foo--bar");        // "FOO_BAR"
RETURN apoc.text.toUpperCase("foo$$bar");        // "FOO_BAR"
RETURN apoc.text.toUpperCase("foo 22 bar");      // "FOO_22_BAR"
----


== Base64 De- and Encoding

Encode or decode a string in base64 or base64Url

.EncodeBase64
[source,cypher]
----
RETURN apoc.text.base64Encode("neo4j") // bmVvNGo=
----

.DecodeBase64
[source,cypher]
----
RETURN apoc.text.base64Decode("bmVvNGo=") // neo4j
----


.EncodeBase64Url
[source,cypher]
----
RETURN apoc.text.base64EncodeUrl("http://neo4j.com/?test=test") // aHR0cDovL25lbzRqLmNvbS8_dGVzdD10ZXN0
----

.DecodeBase64Url
[source,cypher]
----
RETURN apoc.text.base64DecodeUrl("aHR0cDovL25lbzRqLmNvbS8_dGVzdD10ZXN0") // http://neo4j.com/?test=test
----

== Random String

You can generate a random string to a specified length by calling `apoc.text.random` with a length parameter and optional string of valid characters.

The `valid` parameter will accept the following regex patterns, alternatively you can provide a string of letters and/or characters.

[cols="1m,5"]
|===
| Pattern | Description
| A-Z | A-Z in uppercase
| a-z | A-Z in lowercase
| 0-9 | Numbers 0-9 inclusive
|===

The following call will return a random string including uppercase letters, numbers and `.` and `$` characters.

[source,cypher]
----
RETURN apoc.text.random(10, "A-Z0-9.$")
----

== Text Similarity Functions

=== Compare the  strings with the Levenshtein distance

Compare the given strings with the `StringUtils.distance(text1, text2)` method (Levenshtein).

[source,cypher]
----
RETURN apoc.text.distance("Levenshtein", "Levenstein") // 1
----

=== Compare the given strings with the Sørensen–Dice coefficient formula.

.computes the similarity assuming Locale.ENGLISH
[source,cypher]
----
RETURN apoc.text.sorensenDiceSimilarity("belly", "jolly") // 0.5
----

.computes the similarity with an explicit locale
[source,cypher]
----
RETURN apoc.text.sorensenDiceSimilarityWithLanguage("halım", "halim", "tr-TR") // 0.5
----

=== Check if 2 words can be matched in a fuzzy way with `fuzzyMatch`


Depending on the length of the String it will allow more characters that needs to be edited to match the second String.

[source,cypher]
----
RETURN apoc.text.fuzzyMatch("The", "the") // true
----
