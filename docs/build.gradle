import org.neo4j.doc.build.xslt.XsltTask
import org.neo4j.doc.build.docbook.DocBookPlugin

buildscript {
    ext {
        asciidoctorjVersion = '1.5.6'
        asciidoctorjPdfVersion = '1.5.0-alpha.16'
        asciidoctorjDiagramVersion = '1.5.4.1'
        asciidoctorGroovyDslVersion = '1.0.0.Alpha2'
        catalogDir = "$projectDir/docbook/catalog"
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
        classpath "org.asciidoctor:asciidoctorj-pdf:$asciidoctorjPdfVersion"
        classpath "org.asciidoctor:asciidoctorj-diagram:$asciidoctorjDiagramVersion"
        classpath "org.asciidoctor:asciidoctorj-groovy-dsl:$asciidoctorGroovyDslVersion"
        classpath 'net.sf.docbook:docbook-xsl:1.79.1:ns-resources@zip'
        classpath fileTree(dir: "${rootProject.projectDir}/lib", include: '*.jar')
        classpath files(catalogDir)
    }
}

plugins {
    id 'base'
    id 'org.neo4j.doc.build.saxon' version '1.0-alpha02'
    id 'org.neo4j.doc.build.docbook' version '1.0-alpha12'
}

if (!project.hasProperty('apocVersion')) { ext.apocVersion = '5.18.0' }

ext {
    versionParts = apocVersion.split('-')
    branch = '4.0'
    versions = [
        'apoc' : apocVersion,
        'apocDocs' : versionParts[0].split('\\.')[0..1].join('.'),
    ]
    if (1 < versionParts.size()) { versions.apocDocs += '-preview' }

    docsBaseUri = "https://neo4j.com/docs"
    apocBaseUri = "https://neo4j.com/docs/labs/apoc"
    crossReferenceBase = [ // We have no strategy for cross references; always link to current.
            'operations-manual' : "${docsBaseUri}/operations-manual/current",
            'developer-manual'  : "${docsBaseUri}/developer-manual/current",
            'graph-algorithms'  : "${docsBaseUri}/graph-algorithms/current",
            'neo4j-streams'  : "${docsBaseUri}/labs/neo4j-streams/current",
            'apoc'              : "${apocBaseUri}/${versions.apocDocs}",
            'java-reference'    : "${docsBaseUri}/java-reference/current",
            'neo-semantics'  : "${docsBaseUri}/labs/nsmntx/current",
            'rest-docs'         : "${docsBaseUri}/rest-docs/current",
            'ogm-manual'        : "${docsBaseUri}/ogm-manual/current",
            'kerberos-add-on'   : "${docsBaseUri}/add-on/kerberos/current"
    ]
    asciidoctorAttributes = [
        'common': [
            'allow-uri-read'     : 'true',
            'doctype'            : 'book',
            'icons'              : 'font',
            'attribute-missing'  : 'skip',
            'docs-version'       : versions.apocDocs,
            'apoc-release'      : versions.apoc,
            'apoc-release-absolute': versions.apocDocs,
            'branch'             : branch,
            'common-license-page-uri': "$docsBaseUri/license/",
        ],
        'docbook': [
            'imagesdir'          : 'images',
        ],
        'html': [
            'stylesdir'          : 'css',
            'linkcss'            : '',
            'imagesdir'          : 'images',
        ],
        'pdf': [
            'source-highlighter': 'rouge',
            'toc'               : '',
            'showlinks'         : '',
            'nonhtmloutput'     : '',
        ]
    ]
    asciidocSourceDir = "$projectDir/asciidoc"
    distDir = "$buildDir/distributions"
    docId = 'apoc'
}

def getAsciidoctor() {
    if (!project.hasProperty('adoc')) {
        ext.adoc = org.asciidoctor.Asciidoctor.Factory.create()
        asciidoctor.requireLibrary("asciidoctor-diagram")
    }
    ext.adoc
}

task docbook {
    description 'Build DocBook.'
    ext.sourceDir = asciidocSourceDir
    ext.sourceFile = file("$sourceDir/index.adoc")
    ext.outputDir = "$buildDir/docbook/${versions.apocDocs}"
    inputs.dir sourceDir
    outputs.dir outputDir
    doLast {
        def attrs = asciidoctorAttributes.common + asciidoctorAttributes.docbook
        def opts = org.asciidoctor.OptionsBuilder.options()
                .backend('docbook5')
                .safe(org.asciidoctor.SafeMode.UNSAFE)
                .toDir(file(outputDir))
                .mkDirs(true)
                .attributes(attrs)
        getAsciidoctor().convertFile(sourceFile, opts.get())
    }
    doLast {
        copy {
            from(projectDir) {
                include 'images/**'
            }
            into outputDir
        }
    }
    finalizedBy 'preprocess'
}

preprocess {
    dependsOn docbook
    def sourceFile = "${buildDir}/docbook/${versions.apocDocs}/index.xml"
    input sourceFile
    outFile sourceFile
    parameters([
        "book-id": "$docId"
    ])
}

task makeToc(type: XsltTask, dependsOn: docbook) {
    description 'Create a table of contents aka complete content map.'
    ext.sourceDir = docbook.outputDir
    ext.sourceFile = "$sourceDir/index.xml"
    ext.outputFile = "$buildDir/docbook/${versions.apocDocs}/toc.xml"
    def outputDir = "$buildDir/docbook/${versions.apocDocs}/_trash"

    def url = DocBookPlugin.getClassLoader().getResource("xsl/create-toc/xhtml/maketoc.xsl")
    stylesheet url
    input sourceFile
    outFile outputFile

    sourceSaxParser "org.apache.xml.resolver.tools.ResolvingXMLReader"
    stylesheetSaxParser "org.apache.xml.resolver.tools.ResolvingXMLReader"
    uriResolver "org.apache.xml.resolver.tools.CatalogResolver"

    usingUrls true
    usingClasspathUrls true
    if (hasProperty('traceDocbook')) { verbose true }

    parameters([
        "base.dir": "${outputDir}",
        "chunk.section.depth": "8",
        "chunk.first.sections": "1",
        "use.id.as.dirname": "1",
    ])
}

html {
    dependsOn docbook
    ext.sourceDir = docbook.outputDir
    ext.sourceFile = "$sourceDir/index.xml"
    ext.outputDir = "$buildDir/html/${versions.apocDocs}"
    ext.contentMap = "$projectDir/docbook/content-map.xml"
    inputs.file sourceFile
    inputs.dir 'css'
    inputs.dir 'images'
    inputs.dir 'javascript'
    outputs.dir outputDir

    def libraryLinks = [
            "APOC=${crossReferenceBase['apoc']}/",
            "Neo4j_Streams=${crossReferenceBase['neo4j-streams']}/",
            "Neo_Semantics=${crossReferenceBase['neo-semantics']}/",
            "Graph_Algorithms=${crossReferenceBase['graph-algorithms']}/",
            "Developer_Manual=${crossReferenceBase['developer-manual']}/",
    ].join(" ")
    def scripts = [
//        '//ajax.googleapis.com/ajax/libs/jquery/2.2.0/jquery.min.js',
        '//code.jquery.com/jquery-1.12.4.js',
        '//cdn.datatables.net/1.10.13/js/jquery.dataTables.min.js',
        '//maxcdn.bootstrapcdn.com/bootstrap/3.3.6/js/bootstrap.min.js',
        '//cdnjs.cloudflare.com/ajax/libs/codemirror/5.11.0/codemirror.min.js',
        '//cdnjs.cloudflare.com/ajax/libs/codemirror/5.11.0/addon/runmode/runmode.min.js',
        '//cdnjs.cloudflare.com/ajax/libs/codemirror/5.11.0/mode/cypher/cypher.min.js',
        'javascript/datatable.js',
        'javascript/colorize.js',
        'javascript/tabs-for-chunked.js',
        'javascript/mp-nav.js',
        'javascript/versionswitcher.js',
        'javascript/version.js',
        '//s3.amazonaws.com/feedback.neo4jlabs.com/feedback.js',
        '//s3-eu-west-1.amazonaws.com/alpha.neohq.net/docs/new-manual/assets/search.js',
    ].join(" ")
    def styles = [
        '//maxcdn.bootstrapcdn.com/bootstrap/3.3.6/css/bootstrap.min.css',
        '//cdn.datatables.net/1.10.13/css/jquery.dataTables.min.css',
        '//cdnjs.cloudflare.com/ajax/libs/font-awesome/4.1.0/css/font-awesome.min.css',
        '//fonts.googleapis.com/css?family=Open+Sans:400,300,400italic,600,300italic',
        '//cdnjs.cloudflare.com/ajax/libs/codemirror/5.11.0/codemirror.min.css',
        '//cdnjs.cloudflare.com/ajax/libs/codemirror/5.11.0/theme/neo.min.css',
        'css/chunked-base.css',
        'css/extra.css'
    ].join(" ")

    def javascript =
        '''\

        $(document).ready(function() {
          CodeMirror.colorize();
          tabTheSource($('body'));
          var $header = $('header').first();
          $header.prepend(
            $('<a href="https://neo4j.com/docs/labs/" id="logo"><img src="https://s3.amazonaws.com/dev.assets.neo4j.com/wp-content/uploads/20190802073913/labs-logo6.png" alt="Neo4j Labs Logo"/></a>')
          );
          var sidebar = $('<div id=\"sidebar-wrapper\"/>');
          $.get('toc.html', function (d){
            $(d).appendTo(sidebar);
            highlightToc();
            highlightLibraryHeader();
          });
          sidebar.insertAfter($('header').first());
          
        });

        '''


    stylesheet "classpath:xsl/chunk-from-toc/xhtml/chunktoc.xsl"
    input sourceFile

    // Stylesheet parameters
    parameters ([
        "base.dir": "${outputDir}",
        "html.ext": ".html",
        "make.clean.html": "1",
        "chunker.output.method": "html",
        "chunker.output.indent": "yes",
        "use.id.as.dirname": "1",
        "neo.documentation.library.links": libraryLinks,
        "generate.toc": "''",
        "header.rule": "0",
        "neo.newsearch": "1",
        "html.script": scripts,
        "html.stylesheet": styles,
        "chunk.toc": "${contentMap}",
        "current.docid": "$docId",
        "neo.embedded.javascript": javascript
    ])
    doLast {
        copy {
            from(docbook.outputDir) {
                include 'images/**'
            }
            from(projectDir) {
                include 'javascript/**'
                include 'css/**'
            }
            into outputDir
        }
    }
}

task asciidoctorHtml {
    description 'Build HTML'
    ext.sourceDir = asciidocSourceDir
    ext.sourceFile = file("$sourceDir/index.adoc")
    ext.outputDir = "$buildDir/asciidoctor-html/${versions.apocDocs}"
    inputs.dir sourceDir
    outputs.dir outputDir
    doLast {
        def attrs = asciidoctorAttributes.common + asciidoctorAttributes.html
        def opts = org.asciidoctor.OptionsBuilder.options()
                .backend('html5')
                .safe(org.asciidoctor.SafeMode.UNSAFE)
                .toDir(file(outputDir))
                .mkDirs(true)
                .attributes(attrs)
        getAsciidoctor().convertFile(sourceFile, opts.get())
    }
    doLast {
        copy {
            from(projectDir) {
                include 'images/**'
            }
            into outputDir
        }
    }
}

task packageHtml(type: Tar, dependsOn: html) {
    description 'Package HTML for distribution.'
    baseName 'apoc'
    extension 'tar.gz'
    version versions.apocDocs
    compression = Compression.GZIP
    from { html }
    into "$baseName/$version"
    destinationDir file(distDir)
}

task pdf {
    description 'Build PDF.'
    ext.sourceDir = asciidocSourceDir
    ext.sourceFile = file("$sourceDir/index.adoc")
    ext.outputDir = "$buildDir/pdf"
    ext.outputFilename = "neo4j-apoc-${versions.apocDocs}.pdf"
    ext.outputFile = "$outputDir/$outputFilename"
    ext.imagesDir = "$outputDir/images"
    ext.imagesOutDir = imagesDir
    inputs.dir sourceDir
    outputs.file outputFile
    doFirst {
        copy {
            from (projectDir) {
                include 'images/*'
            }
            into(outputDir)
        }
    }
    doLast {
        // asciidoctor-diagram 1.5.4.1 breaks asciidoctor-pdf: using OptionsBuilder#toFile() (-o, --out-file), the file path
        // is created as a directory, and 'imagesoutdir' is resolved wrongly.
        // Work around by using #toDir (-D, --destination-dir) and manually renaming the output file.
        // See https://github.com/asciidoctor/asciidoctor-pdf/issues/842
        def attrs = asciidoctorAttributes.common + asciidoctorAttributes.pdf + [
            'imagesdir'    : imagesDir.toString(),
            'imagesoutdir' : imagesOutDir.toString(),
        ]
        def opts = org.asciidoctor.OptionsBuilder.options()
                .backend('pdf')
                .safe(org.asciidoctor.SafeMode.UNSAFE)
                // .toFile(file(outputFile))                // workaround
                .toDir(file(outputDir))                     // workaround
                .mkDirs(true)
                .attributes(attrs)
        getAsciidoctor().convertFile(sourceFile, opts.get())
        file("$outputDir/index.pdf").renameTo(outputFile)   // workaround
    }
}

// vim: set fdm=expr:
