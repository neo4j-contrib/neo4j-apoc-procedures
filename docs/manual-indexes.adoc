= Manual Indexes

== Data Used

In the below examples I used the flight data setup from the following link. The link guides with loading the complete data set.

https://github.com/nicolewhite/neo4j-flights

However I have pasted a sample subset of the data that can be load to try the example queries below:

----
begin
CREATE (:`Airport` {`abbr`:"SLC", `id`:14869, `name`:"SALT LAKE CITY INTERNATIONAL"});
CREATE (:`Airport` {`abbr`:"OAK", `id`:13796, `name`:"METROPOLITAN OAKLAND INTERNATIONAL"});
CREATE (:`Airport` {`abbr`:"BUR", `id`:10800, `name`:"BOB HOPE"});
CREATE (:`Flight`:`UNIQUE IMPORT LABEL` {`flight_num`:6147, `day`:2, `month`:1, `weekday`:6, `year`:2016, `UNIQUE IMPORT ID`:233903});
CREATE (:`Flight`:`UNIQUE IMPORT LABEL` {`flight_num`:6147, `day`:2, `month`:1, `weekday`:6, `year`:2016, `UNIQUE IMPORT ID`:235869});
CREATE (:`Flight`:`UNIQUE IMPORT LABEL` {`flight_num`:6147, `day`:9, `month`:1, `weekday`:6, `year`:2016, `UNIQUE IMPORT ID`:265364});
CREATE (:`Flight`:`UNIQUE IMPORT LABEL` {`flight_num`:6147, `day`:16, `month`:1, `weekday`:6, `year`:2016, `UNIQUE IMPORT ID`:281968});
CREATE (:`Flight`:`UNIQUE IMPORT LABEL` {`flight_num`:6147, `day`:23, `month`:1, `weekday`:6, `year`:2016, `UNIQUE IMPORT ID`:306386});
CREATE (:`Flight`:`UNIQUE IMPORT LABEL` {`flight_num`:6147, `day`:30, `month`:1, `weekday`:6, `year`:2016, `UNIQUE IMPORT ID`:328834});
commit
begin
CREATE INDEX ON :`Airport`(`abbr`);
CREATE INDEX ON :`Flight`(`flight_num`);
CREATE CONSTRAINT ON (node:`Airport`) ASSERT node.`id` IS UNIQUE;
CREATE CONSTRAINT ON (node:`UNIQUE IMPORT LABEL`) ASSERT node.`UNIQUE IMPORT ID` IS UNIQUE;
commit
schema await
begin
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:233903}), (n2:`Airport`{`id`:13796}) CREATE (n1)-[:`DESTINATION` {`arr_delay`:-13, `taxi_time`:9}]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:235869}), (n2:`Airport`{`id`:10800}) CREATE (n1)-[:`DESTINATION` {`arr_delay`:-8, `taxi_time`:4}]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:265364}), (n2:`Airport`{`id`:14869}) CREATE (n1)-[:`DESTINATION` {`arr_delay`:-30, `taxi_time`:4}]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:281968}), (n2:`Airport`{`id`:14869}) CREATE (n1)-[:`DESTINATION` {`arr_delay`:-21, `taxi_time`:3}]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:306386}), (n2:`Airport`{`id`:14869}) CREATE (n1)-[:`DESTINATION`]->(n2);
MATCH (n1:`UNIQUE IMPORT LABEL`{`UNIQUE IMPORT ID`:328834}), (n2:`Airport`{`id`:14869}) CREATE (n1)-[:`DESTINATION` {`arr_delay`:3, `taxi_time`:7}]->(n2);
commit
begin
MATCH (n:`UNIQUE IMPORT LABEL`)  WITH n LIMIT 10000 REMOVE n:`UNIQUE IMPORT LABEL` REMOVE n.`UNIQUE IMPORT ID`;
commit
begin
DROP CONSTRAINT ON (node:`UNIQUE IMPORT LABEL`) ASSERT node.`UNIQUE IMPORT ID` IS UNIQUE;
commit
----

== Adding Manual Index on Node Property


In order to create manual index on node property , one will have to to use the the APOC stored procedure - `apoc.index.addNode`.

----
match (f:Flight)
call apoc.index.addNode(f,["flight_num"])
return count(f)
----

The above statement will create the node index with the same name as the Label name in this case `Flight` and add the nodes to the index.

Once this has been added check if the node index exists using the stored procedure `apoc.index.list`.

//Check if any legacy index exists
----
CALL apoc.index.list()
----

Better option would be to use the `apoc.index.addNode` as part of the loading process or `LOAD CSV` script.

Output:

image::https://github.com/neo4j-contrib/neo4j-apoc-procedures/tree/master/docs/img/apoc-manual-indexes-nodes.png[width=500]


Once the node index is created we can start using it.
Here are some examples.

The below query returns distinct Airport and City for Flight Number `43`.
Query can be written with the help of stored procedure `apoc.index.nodes` as given below.

----
call apoc.index.nodes('Flight','flight_num:6147') YIELD node
match (node)-[:DESTINATION]->(a:Airport)
return distinct a.name limit 10
----

To Remove the node index `Flight` created -

----
CALL apoc.index.remove('Flight')
----

== Adding Manual Index on Relationship Property


In order to create manual index on relationship property , one will have to to use the the APOC stored procedure - `apoc.index.addRelationship`.

----
match (f:Flight)-[r:DESTINATION]->(a:Airport)
call apoc.index.addRelationship(r,["taxi_time"])
return count(r)
----

The above statement will create the relationship index with the same name as Relationship name in this case `DESTINATION` and adds the relationship to the index.

Once this has been added check if the relationship index exists using the stored procedure `apoc.index.list`.

//Check if any legacy index exists
----
CALL apoc.index.list()
----

Output:

image::https://github.com/neo4j-contrib/neo4j-apoc-procedures/tree/master/docs/img/apoc-manual-indexes-relationship.png[width=500]


Once the relationship index is created we can start using it.
Here are some examples.

Below query will give me the count of the relationship `DESTINATION` with the property `taxi_time = 11`.
Query can be written with the help of a stored procedure `apoc.index.relationships`.

----
call apoc.index.relationships('DESTINATION','taxi_time:7') YIELD rel
return count(rel)
----

Another example query is to return the Flight Number and the Airport Name for those flights that had the `taxi_time` of 11 mins. The query can be written with the help of stored procedure `apoc.index.relationships` as given below.

----
call apoc.index.relationships('DESTINATION','taxi_time:7') YIELD rel
with rel , startnode(rel) as a, endnode(rel) as b
return a.flight_num, b.name limit 10
----


Below is an example of a query using the stored procedure `apoc.index.in` to get the start nodes for all the `DESTINATION` relationships with incoming nodes to the `Airport` node where Airport is `SAN DIEGO INTERNATIONAL`.

----
match (a:Airport {name:"SALT LAKE CITY INTERNATIONAL"}) with a
call apoc.index.in(a,"DESTINATION","taxi_time:7") YIELD node
return node
----

Similarly here is an example of a query using stored procedure `apoc.index.out`. This query gets the Airport Name i.e. end nodes for the relationship `DESTINATION` for all the flight nodes with `flight_num = 43` .

----
match (a:Flight {flight_num:6147}) with a
call apoc.index.out(a,"DESTINATION","taxi_time:7") YIELD node
return node
----

To Remove the relationship index `DESTINATION` created -

----
CALL apoc.index.remove('DESTINATION')
----

