We need to create a unique constraint for the label and property on which we want to add UUIDs.

If we try to setup UUID creation on nodes with the `Person` label without adding a constraint, we'll get an exception as shown below:

[source,cypher]
----
CALL apoc.uuid.install("Person");
----

.Results
|===
| Failed to invoke procedure `apoc.uuid.install`: Caused by: java.lang.RuntimeException: No constraint found for label: Person, please add the constraint with the following : `CREATE CONSTRAINT ON (person:Person) ASSERT person.uuid IS UNIQUE`
|===

We can create a constraint on the `(Person, uuid)` label/property pair by running the following query:

[source,cypher]
----
CREATE CONSTRAINT ON (person:Person)
ASSERT person.uuid IS UNIQUE;
----

And now we can automatically add UUIDs to all new nodes, as well as existing nodes, by running the following query:

[source,cypher]
----
CALL apoc.uuid.install("Person");
----

[NOTE]
====

By default, UUID values will be added to existing nodes and will override existing values.
We can pass the config `addToExistingNodes: false` to only have UUIDs added to new nodes.
====


.Results
|===
| batchComputationResult                                                                                                                                                                                                                                                                          | label    | installed | properties
| {failedParams: {}, committedOperations: 3, batch: {total: 1, committed: 1, failed: 0, errors: {}}, wasTerminated: FALSE, batches: 1, timeTaken: 0, retries: 0, errorMessages: {}, total: 3, operations: {total: 3, committed: 3, failed: 0, errors: {}}, failedOperations: 0, failedBatches: 0} | "Person" | TRUE      | {uuidProperty: "uuid"}
|===

Now let's create a new `Person` node;

[source,cypher]
----
CREATE (:Person {name: "Tom Hanks"});
----

And if we look for all `Person` nodes, we'll see it has a `uuid` property:

[source,cypher]
----
MATCH (p:Person {name: "Tom Hanks"})
RETURN p;
----

.Results
[opts="header"]
|===
| p
| (:Person {name: "Tom Hanks", uuid: "cec34337-9709-46af-bbb7-9e0742d8aaa7"})
|===

The `uuid` property will be created also with a label `SET` when the `addToSetLabels` configuration is set to true. For example:

[source,cypher]
----
CREATE (:AnotherLabel {name: "Tom Hanks"});
// ...
MATCH (n:AnotherLabel) SET n:Person;
----

If we want to use a different property key for our UUID value, we can pass in the `uuidProperty` key, not forgetting to first setup a constraint:

[source,cypher]
----
CREATE CONSTRAINT ON (person:Person)
ASSERT person.myUUID IS UNIQUE;
----

[source,cypher]
----
CALL apoc.uuid.install("Person", {uuidProperty: "myUUID"});
----

.Results
|===
| batchComputationResult                                                                                                                                                                                                                                                                          | label    | installed | properties
| {failedParams: {}, committedOperations: 1, batch: {total: 1, committed: 1, failed: 0, errors: {}}, wasTerminated: FALSE, batches: 1, timeTaken: 0, retries: 0, errorMessages: {}, total: 1, operations: {total: 1, committed: 1, failed: 0, errors: {}}, failedOperations: 0, failedBatches: 0} | "Person" | TRUE      | {uuidProperty: "myUUID"}
|===

And now let's find those `Person` nodes again:

[source,cypher]
----
MATCH (p:Person {name: "Tom Hanks"})
RETURN p;
----

.Results
[opts="header"]
|===
| p
| (:Person {name: "Tom Hanks", uuid: "cec34337-9709-46af-bbb7-9e0742d8aaa7", myUUID: "d09f177b-ff91-4eb9-aac0-73e7a850c9ba"})
|===
