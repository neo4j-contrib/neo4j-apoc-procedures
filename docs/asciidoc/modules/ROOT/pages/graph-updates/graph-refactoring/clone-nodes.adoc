[[clone-nodes]]
= Clone nodes
:description: This section describes procedures that can be used to clone nodes and relationships.



The available procedures are described in the table below:
[separator=¦,opts=header,cols="5,1m,1m"]
|===
¦Qualified Name¦Type¦Release
include::example$generated-documentation/apoc.refactor.cloneNodes.adoc[]
include::example$generated-documentation/apoc.refactor.cloneNodesWithRelationships.adoc[]
|===

NOTE: The `apoc.refactor.cloneNodesWithRelationships(<nodes>)` it's been deprecated in favor of `apoc.refactor.cloneNodes(<nodes>, true)`.

== Example Usage

The examples below will help us learn how to use these procedures.

=== Clone nodes only

.The following creates a graph with two nodes, Foo and Bar:
[source,cypher]
----
CREATE (f:Foo{name:'Foo'}),(b:Bar{name:'Bar'})
----

image::apoc.refactor.cloneNodes.dataset.png[scaledwidth="100%"]

.The following creates copies of both of these nodes:
[source,cypher]
----
MATCH (f:Foo{name:'Foo'}),(b:Bar{name:'Bar'})
CALL apoc.refactor.cloneNodes([f,b])
YIELD input, output, error
RETURN *
----

where `input` is the source node id, `output` is the node cloned and `error` is the possible error message.

If we execute this query, it will result in the following graph:

image::apoc.refactor.cloneNodes.png[scaledwidth="100%"]

In case of error, the message will be thrown in the `error` result, for example with this constraint:
[source,cypher]
----
CREATE CONSTRAINT ON (n:UniqueLabel) ASSERT n.key IS UNIQUE
----

We can create this node:

[source,cypher]
----
CREATE (:UniqueLabel {key: 1})
----

and then we can execute:

[source,cypher]
----
MATCH (n:UniqueLabel) WITH n
CALL apoc.refactor.cloneNodes([n]) 
YIELD error, output RETURN error, output
----

getting as a result:

.Results
[opts="header",cols="1"]
|===
| error | output
| "Node(<NNN>) already exists with label &#96;UniqueLabel&#96; and property &#96;key&#96; = 1" | null
|===


=== Clone nodes with relationships

.The following creates a graph containing two different nodes of type `Actor` connected with other two different node of type `Movie`

[source,cypher]
----
CREATE (k:Actor {name:'Keanu Reeves'})-[:ACTED_IN {role:'Neo'}]->(m:Movie {title:'The Matrix'}),
       (t:Actor {name:'Tom Hanks'})-[:ACTED_IN {role:'Forrest'}]->(f:Movie {title:'Forrest Gump'})
RETURN *
----

image::apoc.refactor.cloneNodesWithRelationships.dataset.png[scaledwidth="100%"]

.The following creates copies of both of these nodes and their relationships:

[source,cypher]
----
MATCH (k:Actor {name:'Keanu Reeves'}) 
MATCH (t:Actor {name:'Tom Hanks'})
CALL apoc.refactor.cloneNodes([k,t], true)
YIELD output
MATCH p=(output)--()
RETURN *
----

As result we have a copy of the nodes and relationships

image::apoc.refactor.cloneNodesWithRelationships.png[scaledwidth="100%"]


=== Clone nodes skipping properties

We can clone nodes excluding some properties, by specifying the `propertyKey`s list as the third parameter
For example, with this node:

[source,cypher]
----
CREATE (f:Foo{name:'Bar', surname: 'Baz', age: 66})
----

we can execute:
[source,cypher]
----
MATCH (n:Foo)
CALL apoc.refactor.cloneNodes([n], false, ["age"])
YIELD output 
RETURN properties(output) AS props
----

.Results
[opts="header",cols="1"]
|===
| props
| { "surname": "Baz", "name": "Bar" }
|===
