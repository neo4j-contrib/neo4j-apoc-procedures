[[rename-label-type-property]]
= Rename
:description: This section describes procedures that can be used to rename labels, relationship types, and properties of nodes and relationships.



Procedures set for renaming labels, relationship types, nodes and relationships' properties.
They return the list of eventually impacted constraints and indexes, the user should take care of.

The available procedures are described in the table below:

[separator=¦,opts=header,cols="5,1m,1m"]
|===
¦Qualified Name¦Type¦Release
include::example$generated-documentation/apoc.refactor.rename.label.adoc[]
include::example$generated-documentation/apoc.refactor.rename.type.adoc[]
include::example$generated-documentation/apoc.refactor.rename.nodeProperty.adoc[]
include::example$generated-documentation/apoc.refactor.rename.typeProperty.adoc[]
|===

As the collection of data is processed in batch via `apoc.periodic.iterate` these procedures support the following config parameters:

.Config
[options=header]
|===
| name | type | default | description
| batchSize | Long | 10000 | run the specified number of operation statements in a single tx - params: {_count, _batch}
| parallel | boolean | true | run operation statements in parallel (note that statements might deadlock if conflicting)
| retries | Long | 0 | if the operation statement fails with an error, sleep 100ms and retry until retries-count is reached - param \{_retry}
| batchMode | String | "BATCH" | how data-driven statements should be processed by operation statement. Valid values are:

* "BATCH" - execute operation statement once per batchSize. Operation statement is prefixed with the following, which extracts each field returned in the data-driven statement from the `$_batch` parameter:
[source,cypher]
----
UNWIND $_batch AS _batch
WITH _batch.field1 AS field1, _batch.field2 AS field2
----
* "SINGLE" - execute operation statement one at a time
* "BATCH_SINGLE" - execute operation statement once per batchSize, but leaves unpacking of batch to the operation statement.
The operation query can access the batched values via the `$_batch` parameter.
| concurrency | Long | 50 | number of concurrent tasks are generated when using `parallel:true`
|===


== Example Usage

The examples below will help us learn how to use these procedures.

.The following creates a graph contains nodes with the label `Engineer` connected by `COLLEAGUES` relationships:
[source,cypher]
----
CREATE (mark:Engineer {name: "Mark", city: "London"})
CREATE (jennifer:Engineer {name: "Jennifer", city: "St Louis"})
CREATE (michael:Engineer {name: "Michael", city: "Dresden"})
CREATE (jim:Engineer {name: "Jim", city: "London"})
CREATE (alistair:Engineer {name: "Alistair", city: "London"})

MERGE (jim)-[:COLLEAGUES {since: date("2006-05-01")}]->(alistair)
MERGE (mark)-[:COLLEAGUES {since: date("2018-02-01")}]->(jennifer)
MERGE (mark)-[:COLLEAGUES {since: date("2013-05-01")}]->(michael)
----

If we execute this query, it will result in the following graph:

image::apoc.rename-initial.png[]

[[rename-node-labels]]
=== Renaming node labels

.The following changes the label on Mark, Jennifer, and Michael from `Engineer` to `DevRel`:
[source,cypher]
----
MATCH (person:Engineer)
WHERE person.name IN ["Mark", "Jennifer", "Michael"]
WITH collect(person) AS people
CALL apoc.refactor.rename.label("Engineer", "DevRel", people)
YIELD committedOperations
RETURN committedOperations
----

If we execute this query, it will result in the following graph:

image::apoc.rename-update-node-labels.png[]

[[rename-relationship-types]]
=== Renaming relationship types

.The following changes the relationship type between Jim and Alistair from `COLLEAGUES` to `FROLLEAGUES`:
[source,cypher]
----
MATCH (:Engineer {name: "Jim"})-[rel]->(:Engineer {name: "Alistair"})
WITH collect(rel) AS rels
CALL apoc.refactor.rename.type("COLLEAGUES", "FROLLEAGUES", rels)
YIELD committedOperations
RETURN committedOperations
----


image::apoc.rename-rename-rel-type.png[]

[[rename-node-properties]]
=== Renaming node properties


.The following query changes the node property `city` to `location` for all nodes with the `DevRel` label:
[source,cypher]
----
MATCH (person:DevRel)
WITH collect(person) AS people
CALL apoc.refactor.rename.nodeProperty("city", "location", people)
YIELD committedOperations
RETURN committedOperations
----

.The following query returns all the nodes in our graph after this refactoring has been done:
[source,cypher]
----
MATCH (n)
RETURN (n)
----

.Results
[opts="header",cols="1"]
|===
| n
| (:DevRel {name: "Jennifer", location: "St Louis"})
| (:DevRel {name: "Michael", location: "Dresden"})
| (:Engineer {city: "London", name: "Jim"})
| (:DevRel {name: "Mark", location: "London"})
| (:Engineer {city: "London", name: "Alistair"})
|===


[[rename-relationship-properties]]
=== Renaming relationship properties


.The following query changes the relationship property `since` to `from` for all relationships:
[source,cypher]
----
MATCH ()-[rel]->()
WITH collect(rel) AS rels
CALL apoc.refactor.rename.typeProperty("since", "from", rels)
YIELD committedOperations
RETURN committedOperations
----

.The following query returns all the paths in our graph after this refactoring has been done:
[source,cypher]
----
MATCH path = ()-[]->()
RETURN path
----

.Results
[opts="header",cols="1"]
|===
| path
|[{"name":"Mark","location":"London"},{"from":"2018-02-01"},{"name":"Jennifer","location":"St Louis"}]
|[{"name":"Mark","location":"London"},{"from":"2013-05-01"},{"name":"Michael","location":"Dresden"}]
|[{"name":"Jim","city":"London"},{"from":"2006-05-01"},{"name":"Alistair","city":"London"}]
|===
