[[virtual-nodes-rels]]
= Virtual Nodes/Rels
:description: This section describes procedures that create virtual nodes and relationships.




Virtual Nodes and Relationships don't exist in the graph, they are only returned to the UI/user for representing a graph projection.
They can be visualized or processed otherwise.
Please note that they have negative id's.

== Function Overview

[separator=¦,opts=header,cols="5,1m,1m"]
|===
¦Qualified Name¦Type¦Release

¦xref::overview/apoc.create/apoc.create.vNode.adoc[apoc.create.vNode icon:book[]] +
`CALL apoc.create.vNode(['Label'], {key:value,...}) YIELD node` - returns a virtual node
¦label:procedure[]
¦label:apoc-core[]


¦xref::overview/apoc.create/apoc.create.vNode.adoc[apoc.create.vNode icon:book[]] +
`apoc.create.vNode(['Label'], {key:value,...})` - function returns a virtual node
¦label:function[]
¦label:apoc-core[]


include::example$generated-documentation/apoc.create.vNodes.adoc[]

¦xref::overview/apoc.create/apoc.create.virtual.fromNode.adoc[apoc.create.virtual.fromNode icon:book[]] +
`apoc.create.virtual.fromNode(node, [propertyNames])` - function returns a virtual node  built from an existing node with only the requested properties
¦label:function[]
¦label:apoc-core[]

¦xref::overview/apoc.create/apoc.create.vRelationship.adoc[apoc.create.vRelationship icon:book[]] +
`CALL apoc.create.vRelationship(nodeFrom,'KNOWS',{key:value,...}, nodeTo) YIELD rel` - returns a virtual relationship
¦label:procedure[]
¦label:apoc-core[]

¦xref::overview/apoc.create/apoc.create.vRelationship.adoc[apoc.create.vRelationship icon:book[]] +
`apoc.create.vRelationship(nodeFrom,'KNOWS',{key:value,...}, nodeTo) YIELD rel` - returns a virtual relationship
¦label:function[]
¦label:apoc-core[]

include::example$generated-documentation/apoc.create.virtualPath.adoc[]
|===

// * TODO `CALL apoc.create.vGraph([nodes, {_labels:[],... prop:value,...}], [rels,{_from:keyValueFrom,_to:{_label:,_key:,_value:value}, _type:'KNOWS', prop:value,...}],['pk1','Label2:pk2'])

== Virtual Nodes/Rels Example

.Virtual node and virtual relationship `vNode`, `vRelationship`

.Simple example aggregate Relationships
[source,cypher]
----
MATCH (from:Account)-[:SENT]->(p:Payment)-[:RECEIVED]->(to:Account)
RETURN from, to, apoc.create.vRelationship(from,'PAID',{amount:sum(p.amount)},to) as rel;
----


.Example with virtual node lookups, people grouped by their countries
[source,cypher]
----
MATCH (p:Person) WITH collect(distinct p.country) as countries
WITH [cName IN countries | apoc.create.vNode(['Country'],{name:cName})] as countryNodes
WITH apoc.coll.groupBy(countryNodes,'name') as countries
MATCH (p1:Person)-[:KNOWS]->(p2:Person)
WITH p1.country as cFrom, p2.country as cTo, count(*) as count, countries
RETURN countries[cFrom] as from, countries[cTo] as to, apoc.create.vRelationship(countries[cFrom],'KNOWS',{count:count},countries[cTo]) as rel;
----

That's of course easier with `apoc.nodes.group`.

From a simple dataset

[source,cypher]
----
CREATE(a:Person)-[r:ACTED_IN]->(b:Movie)
----

We can create a virtual copy, adding as attribute `name` the labels value

[source,cypher]
----
MATCH (a)-[r]->(b)
WITH head(labels(a)) AS l, head(labels(b)) AS l2, type(r) AS rel_type, count(*) as count
CALL apoc.create.vNode([l],{name:l}) yield node as a
CALL apoc.create.vNode([l2],{name:l2}) yield node as b
CALL apoc.create.vRelationship(a,rel_type,{count:count},b) yield rel
RETURN *;
----

image::apoc.create.vRelationshipAndvNode.png[scaledwidth="100%"]

[NOTE]
Virtual nodes and virtual relationships have always a negative id

image::vNodeId.png[scaledwidth="100%"]

Virtual nodes can also be built from existing nodes, filtering the properties in order to get a subset of them.
In this case, the Virtual node keeps the id of the original node.

[source,cypher]
----
MATCH (node:Person {name:'neo', age:'42'})
return apoc.create.virtual.fromNode(node, ['name']) as person
----

.Virtual path `virtualPath`

[source,cypher]
----
CALL apoc.create.virtualPath(['British','Person'],{name:'James', age:28},'KNOWS',{since:2009},['Swedish','Person'],{name:'Daniel', age:30})
----

image::apoc.create.virtualPath.png[scaledwidth="100%"]

We can create a virtual pattern from an existing one

[source,cypher]
----
CREATE(a:Person {name:'Daniel'})-[r:KNOWS]->(b:Person {name:'John'})
----

From this dataset we can create a virtual pattern

[source,cypher]
----
MATCH (a)-[r]->(b)
WITH head(labels(a)) AS labelA, head(labels(b)) AS labelB, type(r) AS rel_type, a.name AS aName, b.name AS bName
CALL apoc.create.virtualPath([labelA],{name: aName},rel_type,{since:2009},[labelB],{name: bName}) yield from, rel, to
RETURN *;
----

//update image once procedure is created
//image::apoc.create.vPatternFullTwo.png[scaledwidth="100%"]