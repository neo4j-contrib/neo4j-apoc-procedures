[[export-csv]]
= Export to CSV
:description: This section describes procedures that can be used to export data in CSV format.



The export CSV procedures export data into a format more supported by Data Science libraries in the Python and R ecosystems.
We may also want to export data into JSON format for importing into other tools or for general sharing of query results.
The procedures described in this section support exporting to a file or as a stream.

For `apoc.export.csv.all`, `apoc.export.csv.data` and `apoc.export.csv.graph`, nodes and relationships properties are ordered alphabetically, using the following structure:

`_id,_labels,<list_nodes_properties_naturally_sorted>,_start,_end,_type,<list_rel_properties_naturally_sorted>`.

For a graph containing node properties `age`, `city`, `kids`, `male`, `name`, and `street` and containing relationship propeties `bar` and `foo`, we'd have the following:

`_id,_labels,age,city,kids,male,name,street,_start,_end,_type,bar,foo`

Labels exported are ordered alphabetically.
The output of `labels()` function is not sorted, use it in combination with `apoc.coll.sort()`.

Note that, to perform a correct Point serialization, it is not recommended to export a point with coordinates x,y and crs: 'wgs-84',
for example `point({x: 56.7, y: 12.78, crs: 'wgs-84'})`. Otherwise, the point will be exported with longitude and latitude (and heigth) instead of x and y (and z)


// tag::export.csv[]
[[export-csv-available-procedures]]
== Available Procedures

The table below describes the available procedures:

[separator=¦,opts=header,cols="5,1m,1m"]
|===
¦Qualified Name¦Type¦Release
include::example$generated-documentation/apoc.export.csv.all.adoc[]
include::example$generated-documentation/apoc.export.csv.data.adoc[]
include::example$generated-documentation/apoc.export.csv.graph.adoc[]
include::example$generated-documentation/apoc.export.csv.query.adoc[]
|===

[[export-csv-file-export]]
== Exporting to a file

include::partial$enableFileExport.adoc[]

[[export-csv-s3-export]]
== Exporting to S3

include::partial$enableS3Export.adoc[]

[[export-csv-stream-export]]
== Exporting a stream

include::partial$streamExport.adoc[]

[[export-csv-examples]]
== Examples

This section includes examples showing how to use the export to CSV procedures.
These examples are based on a movies dataset, which can be imported by running the following Cypher query:

[source,cypher]
----
CREATE (TheMatrix:Movie {title:'The Matrix', released:1999, tagline:'Welcome to the Real World'})
CREATE (Keanu:Person {name:'Keanu Reeves', born:1964})
CREATE (Carrie:Person {name:'Carrie-Anne Moss', born:1967})
CREATE (Laurence:Person {name:'Laurence Fishburne', born:1961})
CREATE (Hugo:Person {name:'Hugo Weaving', born:1960})
CREATE (LillyW:Person {name:'Lilly Wachowski', born:1967})
CREATE (LanaW:Person {name:'Lana Wachowski', born:1965})
CREATE (JoelS:Person {name:'Joel Silver', born:1952})
CREATE
(Keanu)-[:ACTED_IN {roles:['Neo']}]->(TheMatrix),
(Carrie)-[:ACTED_IN {roles:['Trinity']}]->(TheMatrix),
(Laurence)-[:ACTED_IN {roles:['Morpheus']}]->(TheMatrix),
(Hugo)-[:ACTED_IN {roles:['Agent Smith']}]->(TheMatrix),
(LillyW)-[:DIRECTED]->(TheMatrix),
(LanaW)-[:DIRECTED]->(TheMatrix),
(JoelS)-[:PRODUCED]->(TheMatrix);
----

The Neo4j Browser visualization below shows the imported graph:

image::play-movies.png[]

[[export-database-csv]]
=== Export whole database to CSV

The `apoc.export.csv.all` procedure exports the whole database to a CSV file or as a stream.

.The following query exports the whole database to the file `movies.csv`
[source,cypher]
----
CALL apoc.export.csv.all("movies.csv", {})
----

.Results
[opts="header"]
|===
| file         | source                        | format | nodes | relationships | properties | time | rows | batchSize | batches | done | data
| "movies.csv" | "database: nodes(8), rels(7)" | "csv"  | 8     | 7             | 21         | 39   | 15   | 20000     | 1       | TRUE | NULL
|===

The contents of `movies.csv` are shown below:

.movies.csv
[source,csv]
----
"_id","_labels","born","name","released","tagline","title","_start","_end","_type","roles"
"188",":Movie","","","1999","Welcome to the Real World","The Matrix",,,,
"189",":Person","1964","Keanu Reeves","","","",,,,
"190",":Person","1967","Carrie-Anne Moss","","","",,,,
"191",":Person","1961","Laurence Fishburne","","","",,,,
"192",":Person","1960","Hugo Weaving","","","",,,,
"193",":Person","1967","Lilly Wachowski","","","",,,,
"194",":Person","1965","Lana Wachowski","","","",,,,
"195",":Person","1952","Joel Silver","","","",,,,
,,,,,,,"189","188","ACTED_IN","[""Neo""]"
,,,,,,,"190","188","ACTED_IN","[""Trinity""]"
,,,,,,,"191","188","ACTED_IN","[""Morpheus""]"
,,,,,,,"192","188","ACTED_IN","[""Agent Smith""]"
,,,,,,,"193","188","DIRECTED",""
,,,,,,,"194","188","DIRECTED",""
,,,,,,,"195","188","PRODUCED",""
----

.The following query returns a stream of the whole database in the `data` column
[source,cypher]
----
CALL apoc.export.csv.all(null, {stream:true})
YIELD file, nodes, relationships, properties, data
RETURN file, nodes, relationships, properties, data
----

.Results
[opts="header",cols="1m,1m,1m,1m,5"]
|===
| file         | nodes | relationships | properties | data
| NULL | 8     | 7             | 21         | "\"_id\",\"_labels\",\"born\",\"name\",\"released\",\"tagline\",\"title\",\"_start\",\"_end\",\"_type\",\"roles\"
\"188\",\":Movie\",\"\",\"\",\"1999\",\"Welcome to the Real World\",\"The Matrix\",,,,
\"189\",\":Person\",\"1964\",\"Keanu Reeves\",\"\",\"\",\"\",,,,
\"190\",\":Person\",\"1967\",\"Carrie-Anne Moss\",\"\",\"\",\"\",,,,
\"191\",\":Person\",\"1961\",\"Laurence Fishburne\",\"\",\"\",\"\",,,,
\"192\",\":Person\",\"1960\",\"Hugo Weaving\",\"\",\"\",\"\",,,,
\"193\",\":Person\",\"1967\",\"Lilly Wachowski\",\"\",\"\",\"\",,,,
\"194\",\":Person\",\"1965\",\"Lana Wachowski\",\"\",\"\",\"\",,,,
\"195\",\":Person\",\"1952\",\"Joel Silver\",\"\",\"\",\"\",,,,
,,,,,,,\"189\",\"188\",\"ACTED_IN\",\"[\"\"Neo\"\"]\"
,,,,,,,\"190\",\"188\",\"ACTED_IN\",\"[\"\"Trinity\"\"]\"
,,,,,,,\"191\",\"188\",\"ACTED_IN\",\"[\"\"Morpheus\"\"]\"
,,,,,,,\"192\",\"188\",\"ACTED_IN\",\"[\"\"Agent Smith\"\"]\"
,,,,,,,\"193\",\"188\",\"DIRECTED\",\"\"
,,,,,,,\"194\",\"188\",\"DIRECTED\",\"\"
,,,,,,,\"195\",\"188\",\"PRODUCED\",\"\"
"

|===

[[export-nodes-relationships-csv]]
=== Export specified nodes and relationships to CSV


The `apoc.export.csv.data` procedure exports the specified nodes and relationships to a CSV file or as a stream.

.The following query exports all nodes with the `:Person` label with a `name` property that starts with `L` to the file `movies-l.csv`
[source,cypher]
----
MATCH (person:Person)
WHERE person.name STARTS WITH "L"
WITH collect(person) AS people
CALL apoc.export.csv.data(people, [], "movies-l.csv", {})
YIELD file, source, format, nodes, relationships, properties, time, rows, batchSize, batches, done, data
RETURN file, source, format, nodes, relationships, properties, time, rows, batchSize, batches, done, data
----

.Results
[opts="header"]
|===
| file         | source                        | format | nodes | relationships | properties | time | rows | batchSize | batches | done | data
| "movies-l.csv" | "data: nodes(3), rels(0)" | "csv"  | 3     | 0             | 6          | 2    | 3    | 20000     | 1       | TRUE | NULL
|===

The contents of `movies-l.csv` are shown below:

[source,csv]
----
"_id","_labels","born","name","_start","_end","_type"
"191",":Person","1961","Laurence Fishburne",,,
"193",":Person","1967","Lilly Wachowski",,,
"194",":Person","1965","Lana Wachowski",,,
----


.The following query exports all `ACTED_IN` relationships and the nodes with `Person` and `Movie` labels on either side of that relationship to the file `movies-actedIn.csv`
[source,cypher]
----
MATCH (person:Person)-[actedIn:ACTED_IN]->(movie:Movie)
WITH collect(DISTINCT person) AS people, collect(DISTINCT movie) AS movies, collect(actedIn) AS actedInRels
CALL apoc.export.csv.data(people + movies, actedInRels, "movies-actedIn.csv", {})
YIELD file, source, format, nodes, relationships, properties, time, rows, batchSize, batches, done, data
RETURN file, source, format, nodes, relationships, properties, time, rows, batchSize, batches, done, data
----

.Results
[opts="header"]
|===
| file         | source                        | format | nodes | relationships | properties | time | rows | batchSize | batches | done | data
| "movies-actedIn.csv" | "data: nodes(5), rels(4)" | "csv"  | 5     | 4             | 15         | 2    | 9    | 20000     | 1       | TRUE | NULL
|===

The contents of `movies-actedIn.csv` are shown below:

[source,csv]
----
"_id","_labels","born","name","released","tagline","title","_start","_end","_type","roles"
"189",":Person","1964","Keanu Reeves","","","",,,,
"190",":Person","1967","Carrie-Anne Moss","","","",,,,
"191",":Person","1961","Laurence Fishburne","","","",,,,
"192",":Person","1960","Hugo Weaving","","","",,,,
"188",":Movie","","","1999","Welcome to the Real World","The Matrix",,,,
,,,,,,,"189","188","ACTED_IN","[""Neo""]"
,,,,,,,"190","188","ACTED_IN","[""Trinity""]"
,,,,,,,"191","188","ACTED_IN","[""Morpheus""]"
,,,,,,,"192","188","ACTED_IN","[""Agent Smith""]"
----

.The following query returns a stream of all `ACTED_IN` relationships and the nodes with `Person` and `Movie` labels on either side of that relationship in the `data` column
[source,cypher]
----
MATCH (person:Person)-[actedIn:ACTED_IN]->(movie:Movie)
WITH collect(DISTINCT person) AS people, collect(DISTINCT movie) AS movies, collect(actedIn) AS actedInRels
CALL apoc.export.csv.data(people + movies, actedInRels, null, {stream: true})
YIELD file, nodes, relationships, properties, data
RETURN file, nodes, relationships, properties, data
----

.Results
[opts="header",cols="1m,1m,1m,1m,5"]
|===
| file         | nodes | relationships | properties | data
| NULL | 5     | 4             | 15          | "\"_id\",\"_labels\",\"born\",\"name\",\"released\",\"tagline\",\"title\",\"_start\",\"_end\",\"_type\",\"roles\"
\"190\",\":Person\",\"1967\",\"Carrie-Anne Moss\",\"\",\"\",\"\",,,,
\"189\",\":Person\",\"1964\",\"Keanu Reeves\",\"\",\"\",\"\",,,,
\"191\",\":Person\",\"1961\",\"Laurence Fishburne\",\"\",\"\",\"\",,,,
\"192\",\":Person\",\"1960\",\"Hugo Weaving\",\"\",\"\",\"\",,,,
\"188\",\":Movie\",\"\",\"\",\"1999\",\"Welcome to the Real World\",\"The Matrix\",,,,
,,,,,,,\"189\",\"188\",\"ACTED_IN\",\"[\"\"Neo\"\"]\"
,,,,,,,\"190\",\"188\",\"ACTED_IN\",\"[\"\"Trinity\"\"]\"
,,,,,,,\"191\",\"188\",\"ACTED_IN\",\"[\"\"Morpheus\"\"]\"
,,,,,,,\"192\",\"188\",\"ACTED_IN\",\"[\"\"Agent Smith\"\"]\"
"
|===


[[export-graph-csv]]
=== Export virtual graph to CSV

The `apoc.export.csv.graph` procedure exports a xref::virtual/index.adoc[virtual graph] to a CSV file or as a stream.

The examples in this section are based on a virtual graph that contains all `PRODUCED` relationships and the nodes either side of that relationship.
The query below creates a virtual graph and stores it in memory with the name `producers.cached` using xref::misc/static-values.adoc[Static Value Storage].

[source,cypher]
----
MATCH path = (:Person)-[produced:PRODUCED]->(:Movie)
WITH collect(path) AS paths
CALL apoc.graph.fromPaths(paths, "producers", {})
YIELD graph AS g
CALL apoc.static.set("producers.cached", g)
YIELD value
RETURN value, g
----

.Results
[opts="header"]
|===
| value | g
| NULL |{name: "producers", relationships: [[:PRODUCED]], nodes: [(:Person {name: "Joel Silver", born: 1952}), (:Movie {tagline: "Welcome to the Real World", title: "The Matrix", released: 1999})], properties: {}}
|===

.The following query exports the virtual graph from static value storage to the file `movies-producers.csv`
[source,cypher]
----
CALL apoc.export.csv.graph(apoc.static.get("producers.cached"), "movies-producers.csv", {})
----

.Results
[opts="header"]
|===
| file         | source                        | format | nodes | relationships | properties | time | rows | batchSize | batches | done | data
| "movies-producers.csv" | "graph: nodes(2), rels(1)" | "csv"  | 2     | 1             | 5          | 2    | 3    | 20000     | 1       | TRUE | NULL
|===

The contents of `movies-producers.csv` are shown below:

[source,csv]
----
"_id","_labels","born","name","released","tagline","title","_start","_end","_type"
"195",":Person","1952","Joel Silver","","","",,,
"188",":Movie","","","1999","Welcome to the Real World","The Matrix",,,
,,,,,,,"195","188","PRODUCED"
----

.The following query returns a streams of the virtual graph from static value storage to the `data` column
[source,cypher]
----
CALL apoc.export.csv.graph(apoc.static.get("producers.cached"), null, {stream: true})
YIELD file, nodes, relationships, properties, data
RETURN file, nodes, relationships, properties, data
----

.Results
[opts="header",cols="1m,1m,1m,1m,5"]
|===
| file         | nodes | relationships | properties | data
| NULL | 2     | 1             | 5          | "\"_id\",\"_labels\",\"born\",\"name\",\"released\",\"tagline\",\"title\",\"_start\",\"_end\",\"_type\"
\"195\",\":Person\",\"1952\",\"Joel Silver\",\"\",\"\",\"\",,,
\"188\",\":Movie\",\"\",\"\",\"1999\",\"Welcome to the Real World\",\"The Matrix\",,,
,,,,,,,\"195\",\"188\",\"PRODUCED\"
"
|===


[[export-cypher-query-csv]]
=== Export results of Cypher query to CSV

The `apoc.export.csv.query` procedure exports the results of a Cypher query to a CSV file or as a stream.

.The following query exports all `DIRECTED` relationships and the nodes with `Person` and `Movie` labels on either side of that relationship to the file `movies-directed.csv`
[source,cypher]
----
WITH "MATCH path = (person:Person)-[:DIRECTED]->(movie)
      RETURN person.name AS name, person.born AS born,
             movie.title AS title, movie.tagline AS tagline, movie.released AS released" AS query
CALL apoc.export.csv.query(query, "movies-directed.csv", {})
YIELD file, source, format, nodes, relationships, properties, time, rows, batchSize, batches, done, data
RETURN file, source, format, nodes, relationships, properties, time, rows, batchSize, batches, done, data;
----

.Results
[opts="header"]
|===
| file         | source                        | format | nodes | relationships | properties | time | rows | batchSize | batches | done | data
| "movies-directed.csv" | "statement: cols(5)" | "csv"  | 0     | 0             | 10         | 3    | 2    | 20000     | 1       | TRUE | NULL
|===

The contents of `movies-directed.csv` are shown below:

[source,csv]
----
"name","born","role","title","tagline","released"
"Lilly Wachowski","1967","","The Matrix","Welcome to the Real World","1999"
"Lana Wachowski","1965","","The Matrix","Welcome to the Real World","1999"
----

.The following query returns a stream of all `DIRECTED` relationships and the nodes with `Person` and `Movie` labels on either side of that relationship
[source,cypher]
----
WITH "MATCH path = (person:Person)-[:DIRECTED]->(movie)
      RETURN person.name AS name, person.born AS born,
             movie.title AS title, movie.tagline AS tagline, movie.released AS released" AS query
CALL apoc.export.csv.query(query, null, {stream: true})
YIELD file, nodes, relationships, properties, data
RETURN file, nodes, relationships, properties, data;
----

.Results
[opts="header",cols="1m,1m,1m,1m,5"]
|===
| file       | nodes | relationships | properties | data
| NULL | 0     | 0             | 10         | "\"name\",\"born\",\"title\",\"tagline\",\"released\"
  \"Lilly Wachowski\",\"1967\",\"The Matrix\",\"Welcome to the Real World\",\"1999\"
  \"Lana Wachowski\",\"1965\",\"The Matrix\",\"Welcome to the Real World\",\"1999\"
  "
|===
// end::export.csv[]

//When the config `bulkImport` is enable it create a list of file that can be used for Neo4j Bulk Import.
//
//*This config can be used only with `apoc.export.csv.all` and `apoc.export.csv.graph`*
//
//All file create are named as follow:
//
//* Nodes file are construct with the name of the input file append with `.nodes.[LABEL_NAME].csv`
//* Rel file are construct with the name of the input file append with `.relationships.[TYPE_NAME].csv`
//
//If Node or Relationship have more than one Label/Type it will create one file for Label/Type.

//.configuration options
//[options=header]
//|===
//| param | default | description
//| format | gephi | In export to Graphml script define the export format. Possible value is: "gephi"
//| caption | | It's an array of string (i.e. ['name','title']) that define an ordered set of properties eligible as value for the `Label` value, if no match is found the there is a fallback to the node label, if the node label is missing the then the ID is used
//| useTypes | false | Write the attribute type information to the graphml output
//| batchSize | 20000 | define the batch size
// | silent | false | if enabled write progress output
//| delim | "," | define the delimiter character (export csv)
//| arrayDelim | ";" | define the delimiter character for arrays (used in the bulk import)
//| quotes | 'always' a| quote-character used for CSV, possible values are:
//
//* `none`: the same behaviour of the current `false`
//* `always`:  the same behaviour of the current `true`
//* `ifNeeded`: it applies quotes only when it's necessary
//
//| useTypes | false | add type on file header (export csv and graphml export)
//| nodesOfRelationships | false | if enabled add relationship between nodes (export Cypher)
//| storeNodeIds| false | set nodes' ids (import/export graphml)
//| readLabels | false | read nodes' labels (import/export graphml)
//| defaultRelationshipType | "RELATED" | set relationship type (import/export graphml)
//| separateFiles | false | export results in separated file by type (nodes, relationships..)
//| cypherFormat | create | In export to cypher script, define the cypher format (for example use `MERGE` instead of `CREATE`). Possible values are: "create", "updateAll", "addStructure", "updateStructure".
//| bulkImport | true | In export it creates files for Neo4j Admin import
//| separateHeader | false | In export it creates two file one for header and one for data
//| stream | false | stream the xml directly to the client into the `data` field
//|===
