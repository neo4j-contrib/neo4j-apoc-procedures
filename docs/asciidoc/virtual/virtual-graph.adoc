[[virtual-graph]]
== Virtual Graph

////
Virtual Nodes and Relationships don't exist in the graph, they are only returned to the UI/user for representing a graph projection.
They can be visualized or processed otherwise.
Please note that they have negative id's.

[cols="1m,5"]
|===
| CALL apoc.create.vNode(['Label'], {key:value,...}) YIELD node | returns a virtual node
| apoc.create.vNode(['Label'], {key:value,...}) | returns a virtual node
| CALL apoc.create.vNodes(['Label'], [{key:value,...}]) | returns virtual nodes
| CALL apoc.create.vRelationship(nodeFrom,'KNOWS',{key:value,...}, nodeTo) YIELD rel | returns a virtual relationship
| apoc.create.vRelationship(nodeFrom,'KNOWS',{key:value,...}, nodeTo) | returns a virtual relationship
| CALL apoc.create.vPattern({_labels:['LabelA'],key:value},'KNOWS',{key:value,...}, {_labels:['LabelB'],key:value}) | returns a virtual pattern
| CALL apoc.create.vPatternFull(['LabelA'],{key:value},'KNOWS',{key:value,...},['LabelB'],{key:value}) | returns a virtual pattern
| CALL apoc.nodes.group([labels],[properties],[{node-aggregation},{rel-aggregation]) yield nodes, relationships | Group all nodes and their relationships by given keys, create virtual nodes and relationships for the summary information, you can provide an aggregations map for nodes and rels [{kids:'sum',age:['min','max','avg'],gender:'collect'},{`*`,'count'}]
|===

// * TODO `CALL apoc.create.vGraph([nodes, {_labels:[],... prop:value,...}], [rels,{_from:keyValueFrom,_to:{_label:,_key:,_value:value}, _type:'KNOWS', prop:value,...}],['pk1','Label2:pk2'])

== Virtual Graph
////

[abstract]
--
This section describes procedures that create virtual graphs from Cypher queries or JSON documents.
--

Create a graph object (map) from information that's passed in.
It's basic structure is: `{name:"Name",properties:{properties},nodes:[nodes],relationships:[relationships]}`

[cols="1m,5"]
|===
| apoc.graph.from(data,'name',{properties}) yield graph | creates a virtual graph object for later processing it tries its best to extract the graph information from the data you pass in
| apoc.graph.fromData([nodes],[relationships],'name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromPaths(path,'name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromPaths([paths],'name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromDB('name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromCypher('statement',{params},'name',{properties}) | creates a virtual graph object for later processing
| apoc.agg.graph(element) as graph | aggregates graph elements to a "graph" map with unique sets of "nodes" and "relationships"
|===


Create a graph object (map) from information that's passed in.
Its basic structure is: `{name:"Name",properties:{properties},nodes:[nodes],relationships:[relationships]}`

[cols="1m,5"]
|===
| apoc.graph.from(data,'name',{properties}) yield graph | creates a virtual graph object for later processing it tries its best to extract the graph information from the data you pass in
| apoc.graph.fromData([nodes],[relationships],'name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromPaths(path,'name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromPaths([paths],'name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromDB('name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromCypher('statement',{params},'name',{properties}) | creates a virtual graph object for later processing
| apoc.graph.fromDocument({json},{config}) | transform JSON documents into graph structures
| apoc.graph.validateDocument({json},{config}) | validate the JSON and returns information about required fields violations
|===

=== `apoc.graph.fromDocument`

The procedure `apoc.graph.fromDocument` transforms a JSON into a graph structure.
It takes two arguments:

* *json*, _type Object_: the JSON that must be transformed. Every entry must have an `id` and a `type` (name of Label), configurable via the config params. +
The value can be a String, or Cypher Map or List of Maps.
* *config*, _type Map_: the configuration params

Currently spatial and datetime properties are not handled yet.
More advanced configuration for mapping the document is coming in future versions.

The config is composed by the following parameters:

* *write*, _type boolean_: persist the graph otherwise return a Virtual Graph, default *false*
* *labelField*, _type String_: the field name that became the label of the node, default *type*
* *idField*, _type String_: the document field name that will become the id field of the created nodes (used for node resolution when you create relationships between nodes), default *id*
* *generateId*, _type boolean_: in case of missing id-field value it generates an UUID for it, default *true*
* *defaultLabel*, _type String_: in case of missing label-field value is uses the provided default label, default is empty
* *skipValidation*, _type boolean_: in case you want skip the validation process into the `apoc.graph.fromDocument` procedure *false*
* *mappings*, _type Map<String, String>_: you can use a JSON path like syntax for:
** include properties
** defining document properties as value objects, by prepending the `@` to the property name
** define custom/composite keys per Labels, by prepending the `!` to the property name

Following an example of configuration with mappings:

[source, cypher]
----
{
    write: false,
    idField: "id",
    mappings: {
      `$`: 'Person:Reader{*,@size}'
      `$.books`: 'Book{!title, released}'
    }
}
----

Lets describe the mappings:

* **`$`: 'Person:Reader{*,@size}'**: this means that at the root object will be applied
two labels `Person` and `Reader`, all properties are included and the `size` property will be transformed into a value objects
as you can see no id is specified so we will consider as id the property defined into the `idField`
* **`$.books`: 'Book{!title, released}'**: this means that at the `books` property of the root object will transformed
into a node with label Book composed by two properties `title` considered as id (it's marked with `!`) and `released`
moreover the property will be connected to the parent node of type `Person:Reader` via the `BOOKS` relationship

[source, json]
----
{
    "id": 1,
    "type": "artist",
    "name": "Genesis",
    "members": ["Tony Banks", "Mike Rutherford", "Phil Collins"],
    "years": [1967, 1998, 1999, 2000, 2006]
}
----
In this case it create one `Node` with labels *Artist*

It also accepts list of documents:

[source, json]
----
[{
    "id": 1,
    "type": "artist",
    "name": "Genesis",
    "members": ["Tony Banks", "Mike Rutherford", "Phil Collins"],
    "years": [1967, 1998, 1999, 2000, 2006]
}, {
    "id": 2,
    "type": "artist",
    "name": "Daft Punk",
    "members": ["Guy-Manuel de Homem-Christo", "Thomas Bangalter."],
    "years": [1987, 1993, 1999, 2004, 2008, 2011]
}]
----
In this case it create 2 `Node` with labels *Artist*


JSON Tree to graph:

[source, json]
----
{
	"id": 1,
	"type": "artist",
	"name": "Genesis",
	"albums": [{
		"type": "album",
		"id": 1,
		"producer": "Jonathan King",
		"title": "From Genesis to Revelation"
	}]
}
----

In this case it will create 2 `Node`, one *Artist* and one *Album* connected to each other by the *ALBUMS* `Relationship`

=== Virtual Graph Examples

We create a dataset for our examples

[source,cypher]
----
CREATE (a:Actor {name:'Tom Hanks'})-[r:ACTED_IN {roles:'Forrest'}]->(m:Movie {title:'Forrest Gump'})
RETURN *
----

.Virtual graph from data

[source,cypher]
----
MATCH (n)-[r]->(m) CALL apoc.graph.fromData([n,m],[r],'test',{answer:42})
YIELD graph
RETURN *
----

.Virtual graph from path

[source,cypher]
----
MATCH path = (n)-[r]->(m) CALL apoc.graph.fromPath(path,'test',{answer:42})
YIELD graph
RETURN *
----

.Virtual graph from paths

[source,cypher]
----
MATCH path = (n)-[r]->(m) CALL apoc.graph.fromPaths([path],'test',{answer:42})
YIELD graph
RETURN *
----

.Virtual graph from DB

[source,cypher]
----
CALL apoc.graph.fromDB('test',{answer:42})
YIELD graph
RETURN *
----

.Virtual graph from Cypher

[source,cypher]
----
CALL apoc.graph.fromCypher('MATCH (n)-[r]->(m) RETURN *',null,'test',{answer:42})
YIELD graph
RETURN *
----

As a result we have a virtual graph object for later processing

image::apoc.graph.png[width=800]

.Virtual graph from JSON

[source,cypher]
----
CALL apoc.graph.fromDocument("{'id': 1,'type': 'artist','name':'Genesis','members': ['Tony Banks','Mike Rutherford','Phil Collins'],'years': [1967, 1998, 1999, 2000, 2006],'albums': [{'type': 'album','id': 1,'producer': 'Jonathan King','title': 'From Genesis to Revelation'}]}", {write: false})
YIELD graph
RETURN graph
----

As a result we have a virtual graph with two nodes and one relationship:

image::apoc.graph.fromDocument_1[scaledwidth="100%"]


In case this json:
[source, json]
----
{
    "id": 1,
    "type": "Person",
    "name": "Andrea",
    "sizes": {
        "weight": {
            "value": 70,
            "um": "Kg"
        },
        "height": {
            "value": 174,
            "um": "cm"
        }
    }
}
----
You can manage the `sizes` property as value object so you manage it as follows:


[source,cypher]
----
call apoc.graph.validateDocument(<json>, {mappings: {`$`: "Person{*,@sizes}"}})
----

So the procedure will create a node with the following properties:
[source, json]
----
{
    "id": 1,
    "type": "Person",
    "name": "Andrea",
    "sizes.weight.value": 70,
    "sizes.weight.um": "Kg",
    "sizes.height.value": 174,
    "sizes.height.um": "cm"
}
----

As specified you can also provide a set of value-object properties for a Label:

[source,cypher]
----
call apoc.graph.validateDocument(<json>, {mappings: {`$`: "Person{*,@sizes}"}})
----

You can also do a pre-validation over the document with the `apoc.graph.validateDocument` procedure that will return the
record with invalid data.

[source,cypher]
----
call apoc.graph.validateDocument('[{"foo": "foo"}, {"bar": "bar", "id": 1, "type": "label"}, {"fooBar": "fooBar", "id": 1}]')
----

or

[source,cypher]
----
call apoc.graph.validateDocument([{foo: "foo"}, {bar: "bar", id: 1, type: "label"}, {fooBar: "fooBar", id: 1}])
----

Will display the following result:

image::apoc.graph.validateDocument[scaledwidth="100%"]
