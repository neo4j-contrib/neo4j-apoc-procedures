plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.3'  apply false
    id "com.bmuschko.nexus" version "2.3.1"
    id 'maven-publish'
    id 'antlr'
    id "org.sonarqube" version "2.7"
    id "com.github.hierynomus.license-report" version"0.15.0"
}

downloadLicenses {
    excludeDependencies = [
            'org.neo4j.*'
    ]
}

allprojects {
    group = 'org.neo4j.procedure'
    version = '4.4.0.17'
    archivesBaseName = 'apoc'
    description = """neo4j-apoc-procedures"""
}

apply plugin: 'java'

repositories {

    /*maven {  // this contains the neo4j 4.0.0-beta jars
    url "https://neo4j.bintray.com/community/"
}*/
    mavenCentral()
    maven {
        url "https://repo.gradle.org/gradle/libs-releases"
    }
    mavenLocal()
}

subprojects {
    apply plugin: 'java'

    repositories {

        /*maven {  // this contains the neo4j 4.0.0-beta jars
        url "https://neo4j.bintray.com/community/"
    }*/
        mavenCentral()
        maven {
            url "https://repo.gradle.org/gradle/libs-releases"
        }
        mavenLocal()
    }


    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    task mySourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task myJavadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }
    test {
        //exclude '**/CypherProceduresClusterTest.class'//, '**/AtomicTest.class'

        // neo4jDockerImage system property is used in TestContainerUtil
        systemProperties 'user.language' : 'en' ,
                'user.country ' : 'US',
                'neo4jDockerImage' : System.getProperty("NEO4JVERSION") ? 'neo4j:' + System.getProperty("NEO4JVERSION") + '-enterprise' : 'neo4j:4.4.20-enterprise',
                'neo4jCommunityDockerImage': System.getProperty("NEO4JVERSION") ? 'neo4j:' + System.getProperty("NEO4JVERSION") : 'neo4j:4.4.20'

        maxHeapSize = "8G"
        forkEvery = 50
        maxParallelForks = 1 //Runtime.runtime.availableProcessors().intdiv(2) + 1

        // This would apply only to GitHub Actions
        if (System.env.CI != null) {
            minHeapSize = "128m"
            maxHeapSize = "512m"
        }

        // This would apply only to TeamCity
        // We need to ignore the failures because we may have tests muted
        if (System.env.TEAMCITY_VERSION != null) {
            ignoreFailures(true)
            if (project.hasProperty('excludeSeleniumTests')) {
                exclude '**/LoadHtmlTest*'
                exclude '**/LoadHtmlTestParameterized*'
            }
        }

        filter {
            setFailOnNoMatchingTests(false)
        }

        // in TC we don't have the CI env so we can manage it in this way
        if (System.env.TEAMCITY_VERSION != null) {
            ignoreFailures(true)
        }
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'
    }

    configurations {
        apt
    }


    compileJava {
        options.annotationProcessorPath = configurations.apt
        options.compilerArgs += ["-AIgnoreContextWarnings"]
    }

}

apply from: "licenses-3rdparties.gradle"

ext {
    publicDir =  "${project.rootDir}"
    // NB: due to version.json generation by parsing this file, the next line must not have any if/then/else logic
    neo4jVersion = "4.4.20"
    // instead we apply the override logic here
    neo4jVersionEffective = project.hasProperty("neo4jVersionOverride") ? project.getProperty("neo4jVersionOverride") : neo4jVersion
    testContainersVersion = '1.17.6'
}
